plugins {
    id "java"
    id "idea"
    id "jacoco"
    id "checkstyle"
    id "org.springframework.boot" version "2.1.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.spotbugs" version "1.6.9"
    id "org.owasp.dependencycheck" version "1.4.5.1"
    id "com.palantir.docker" version "0.21.0"
    id "com.google.cloud.tools.jib" version "1.0.0"
}

repositories {
    maven {
        url "https://maven.aliyun.com/repository/central"
    }
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

docker {
    dependsOn bootJar
    name "net.thoughtworks/${jar.baseName}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

jib {
    from {
        image = 'openjdk:8-jdk-slim'
    }
    to {
        image = "net.thoughtworks/example-product-service"
    }
    container {
        mainClass = 'net.thoughtworks.Application'
        jvmFlags = ['-Dserver.port=9090']
        ports = ['9090']
    }
}

dependencyCheck {
    autoUpdate = true
    failBuildOnCVSS = 8
    suppressionFile = "$projectDir/config/dependency-check/suppression.xml"
    data {
        directory = "$projectDir/build/dependency-check-data"
        connectionString = 'jdbc:h2:file:%s;FILE_LOCK=FS;AUTOCOMMIT=ON;'
    }
}

checkstyle {
    maxWarnings = 0
    ignoreFailures = false
}

spotbugs {
    excludeFilter = file("$projectDir/config/spotbugs/exclude.xml")
}

spotbugsMain {
    reports {
        xml.enabled false
        html.enabled true
    }
}

jacocoTestReport {
    reports {
        csv.enabled false
        xml.enabled false
        html.enabled true
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

// to run coverage verification during the build (and fail when appropriate)
check.dependsOn jacocoTestCoverageVerification
// explicitly set gradle wrapper version

wrapper {
    gradleVersion = '5.0'
}
