plugins {
    id "java"
    id "idea"
    id "jacoco"
    id "checkstyle"
    id "org.springframework.boot" version "2.1.3.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.spotbugs" version "1.6.9"
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id "com.diffplug.gradle.spotless" version "3.18.0"
    id "org.owasp.dependencycheck" version "4.0.2"
    id "com.palantir.docker" version "0.21.0"
    id "com.google.cloud.tools.jib" version "1.0.0"
}

repositories {
    maven {
        url "https://maven.aliyun.com/repository/central"
    }
    mavenCentral()
}

apply from: "${rootProject.projectDir}/gradle/idea.gradle"
apply from: "${rootProject.projectDir}/gradle/jacoco.gradle"
apply from: "${rootProject.projectDir}/gradle/checkstyle.gradle"
apply from: "${rootProject.projectDir}/gradle/spotbugs.gradle"
apply from: "${rootProject.projectDir}/gradle/git-hooks.gradle"
apply from: "${rootProject.projectDir}/gradle/spotless.gradle"
apply from: "${rootProject.projectDir}/gradle/dependency-check.gradle"

group = 'net.thoughtworks'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) { options.encoding = 'utf-8' }

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile('org.modelmapper:modelmapper:1.1.1')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("io.rest-assured:spring-mock-mvc:3.3.0")
    testCompile('com.tngtech.archunit:archunit:0.9.3')
    testCompile('com.tngtech.archunit:archunit-junit4:0.9.3')
}

bootRun {
    args = ["--spring.profiles.active=local"]
}

bootJar {
    launchScript()
    archiveName = 'example-product-service.jar'
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    'label': System.getenv('CI_COMMIT_TAG') ? System.getenv('CI_COMMIT_TAG') : 'unknown'
            ]
        }
    }
}

docker {
    dependsOn bootJar
    name "net.thoughtworks/${jar.baseName}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

jib {
    from {
        image = 'openjdk:8-jdk-slim'
    }
    to {
        image = "net.thoughtworks/example-product-service"
    }
    container {
        mainClass = 'net.thoughtworks.ProductServiceApplication'
        jvmFlags = ['-Dserver.port=9090']
        ports = ['9090']
    }
}

wrapper {
    gradleVersion = '5.2.1'
}
